managed with unmanaged save implementation in class zbp_dsag_r_classification unique;
strict ( 2 );
with draft;
extensible;
define behavior for ZDSAG_R_CLASSIFICATION alias Classification
with unmanaged save
late numbering
draft table zdsag_class_d
etag master ClassLastChangedDateTime
lock master unmanaged total etag ClassLastChangedDateTime
authorization master ( instance )
extensible

{
  field ( readonly ) ClassInternalID;
  field ( mandatory : create, readonly : update ) ClassType;

  create;
  update;
  delete;

  event ClassificationCreated parameter ZDSAG_A_CLASSIFICATION_CREATE;

  // Early save validations
  validation check_if_classtype_exists on save { create; field ClassType; }
  validation simulate_create on save { create; update; }

  determination setDefaultValues on modify { create; }

  action ( features : instance ) assign_class_to_material parameter ZDSAG_PARAMETER_MATNR_DETAIL result [1] $self;

  side effects
  {
    action assign_class_to_material affects $self;
  }
  association _ClassificationDescription {  create; with draft; }
  association _ClassificationCharacteristic { create; with draft; }

  draft action Edit;
  draft action Resume;
  draft action Activate optimized;
  draft action Discard;

  draft determine action Prepare
  {
    validation check_if_classtype_exists;
    validation simulate_create;
  }
}


define behavior for ZDSAG_R_CLASSDESCRIPTION alias Description
draft table zdsag_desc_d
etag dependent by _Classfications
lock dependent by _Classfications
authorization dependent by _Classfications
late numbering
with unmanaged save
extensible
{
  field ( readonly ) ClassInternalID;
  field ( readonly : update ) Language;
  field ( mandatory ) ClassDescription;
  determination setDefaultValues on modify { create; }

  update ( features : instance );
  delete;
  association _Classfications { with draft; }
}


define behavior for ZDSAG_R_CLASSCHARACTERS alias Characteristics
draft table zdsag_char_d
etag dependent by _Classfications
lock dependent by _Classfications
authorization dependent by _Classfications
late numbering
with unmanaged save
extensible
{
  field ( readonly ) ClassInternalID, CharcPositionNumber;
  field ( mandatory ) CharcInternalID;

  update ( features : instance );
  delete;

  association _Classfications { with draft; }
}